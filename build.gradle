plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group 'com.hcgstudio'
version '0.1.1'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'com.hcgstudio.JImprove'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JImprove'
                description = 'A library to add features to Java.'
                url = 'https://github.com/HCGStudio/JImprove'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'mahoshojoHCG'
                        name = 'Yin Zhaoyu'
                        email = 'hcg@hcg.today'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/HCGStudio/JImprove.git'
                    developerConnection = 'scm:git:ssh://github.com/HCGStudio/JImprove.git'
                    url = 'https://github.com/HCGStudio/JImprove'
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.encoding = "UTF-8" 
    options.charSet = 'UTF-8'
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.2")
    implementation("org.jetbrains:annotations:19.0.0")
}